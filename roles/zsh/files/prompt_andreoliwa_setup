#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_steeef_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_prompt_steeef_colors[6]}%b%f%u%c${_prompt_steeef_colors[4]}●%f"
  else
    branch_format="${_prompt_steeef_colors[6]}%b%f%u%c"
  fi

  # Show stash count if any
  local git_repo=''
  git rev-parse --is-inside-work-tree > /dev/null 2>&1 && git_repo='yes'

  local stash_format=""
  if [[ -n "${git_repo}" ]]; then
      local stash_count="$(git stash list| wc -l | sed -E 's/ +//')"
      if [[ ${stash_count} -ne 0 ]]; then
        local stash_format="${_prompt_steeef_colors[4]}(stashed: ${stash_count})%f"
      fi
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format} ${stash_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function andreoliwa_rprompt {
  RETVAL=$?
  if [[ $RETVAL -ne 0 ]]; then
    print -n "${_prompt_steeef_colors[7]}$RETVAL%f"
  else
    print -n ""
  fi
}

function prompt_steeef_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_steeef_precmd

  # Use extended color pallete if available.
  # http://unix.stackexchange.com/questions/124407/what-color-codes-can-i-use-in-my-ps1-prompt
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_steeef_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{34}" # Dark green
      "%F{227}" # Bold yellow
      "%F{196}" # Bold red
    )
  else
    _prompt_steeef_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
      "%F{yellow}"
      "%F{red}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="${_prompt_steeef_colors[6]}%b%f%u%c"
  local action_format="${_prompt_steeef_colors[5]}%a%f"
  local unstaged_format="${_prompt_steeef_colors[2]}●%f"
  local staged_format="${_prompt_steeef_colors[5]}●%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format ' [%v]'

  # Define prompts.
  PROMPT="${_prompt_steeef_colors[1]}"'$python_info[virtualenv]'"${_prompt_steeef_colors[5]} %n@%m%f${_prompt_steeef_colors[3]} %~%f "'${vcs_info_msg_0_}'"
$ "
  RPROMPT='$(andreoliwa_rprompt)'
}

prompt_steeef_setup "$@"
